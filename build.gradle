import com.github.spotbugs.SpotBugsTask

buildscript {
    ext {
        springVersion = '5.2.3.RELEASE'
        springBootVersion = '2.2.4.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:latest.release'
        classpath 'org.ajoberstar:gradle-git:1.4.2'
    }
}

plugins {
    // Language
    id 'java'
    id 'application'

    // IDEs
    id 'idea'
    id 'eclipse'

    // Frameworks
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'

    // Build Tools
    id 'jacoco'
    id 'com.github.spotbugs' version '3.0.0'
    id 'com.github.psxpaul.execfork' version '0.1.8'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'org.ajoberstar.grgit' version '3.0.0'

    // Package Management
    id 'maven'
    id 'maven-publish'
}

description = 'census-fwmt-outcome-service'
group = 'uk.gov.ons.census.fwmt'
version = rootProject.file('versioning/version.txt').text.trim()

mainClassName = 'uk.gov.ons.census.fwmt.outcomeservice.Application'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()

    maven {
        url 'https://ons.jfrog.io/ons/libs-releases-virtual'
        credentials {
            username "${artifactory_user}"
            password "${artifactory_password}"
        }
    }
    maven {
        url 'https://ons.jfrog.io/ons/libs-snapshots-virtual'
        credentials {
            username "${artifactory_user}"
            password "${artifactory_password}"
        }
    }

    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

dependencyManagement {
    dependencies {
        dependency "org.springframework:spring-core:${springVersion}"
        dependency "org.springframework.boot:spring-boot:${springBootVersion}"
    }
}

dependencies {
    implementation 'com.github.spotbugs:spotbugs-annotations:4.0.1'

	// This events library is specifically for this release. Branch of this release for code changes
    compile('uk.gov.ons.census.fwmt:census-fwmt-events:3.0.51-RELEASE')
	compile('uk.gov.ons.census.fwmt:census-fwmt-common:2.0.2-SNAPSHOT')
	compile('uk.gov.ons.ctp.integration.common:product-reference:1.0.14')
    compile("uk.gov.ons.census.ffa:census-ffa-storage-utils:0.0.4-RELEASE")
    compile('uk.gov.ons.ons.fwmt:ons-fwmt-outcome-test:0.0.2-SNAPSHOT')

    compile("javax.activation:activation:1.1.1")

	compile('org.projectlombok:lombok')
	annotationProcessor('org.projectlombok:lombok')
	testAnnotationProcessor('org.projectlombok:lombok')

	compile('org.springframework.retry:spring-retry')
	compile('org.springframework:spring-aop')

	compile('org.aspectj:aspectjweaver')
    compile('ma.glasnost.orika:orika-core:1.5.4')
    runtimeOnly 'org.postgresql:postgresql'


	compile('org.springframework.boot:spring-boot-starter')
	compile('org.springframework.boot:spring-boot-starter-amqp')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-security')

	compile('io.springfox:springfox-swagger2:2.9.2')
	compile('io.springfox:springfox-swagger-ui:2.9.2')
	compile('io.springfox:springfox-bean-validators:2.9.2')

    compile 'org.bouncycastle:bcprov-jdk15on:1.61'
    compile 'org.bouncycastle:bcpg-jdk15on:1.61'

    compile('org.freemarker:freemarker:2.3.28')

	compileOnly('org.projectlombok:lombok')

	testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')

	compile('org.eclipse.persistence:org.eclipse.persistence.moxy:2.5.0')
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    compileOnly 'net.jcip:jcip-annotations:1.0'

    // Mocking Objects
    testCompile 'org.mockito:mockito-core'
    testCompile 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.mockito:mockito-inline:2.13.0'
}

artifactory {
	contextUrl = System.getenv('ARTIFACTORY_URL')
	publish {
		repository {
			repoKey = 'libs-snapshot-local'
			username = System.getenv('ARTIFACTORY_USERNAME')
			password = System.getenv('ARTIFACTORY_PASSWORD')
		}
	}
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'Full'
        showStandardStreams = true
    }
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

artifactory {
    contextUrl = 'https://ons.jfrog.io/ons/'
    publish {
        repository {
            repoKey = 'libs-releases-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        defaults {
            publishBuildInfo = false
        }
    }
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    'Git-Commit': grgit.head().getAbbreviatedId(8)
            ]
        }
    }
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = group
			artifactId = 'census-fwmt-outcome-service'
			version = version

			from components.java
		}
	}
}

artifactoryPublish {
	publications('maven')
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = false
	}
}
test.finalizedBy(project.tasks.jacocoTestReport)

check.dependsOn jacocoTestReport

spotbugs {
    toolVersion = '3.1.12'
}

tasks.withType(SpotBugsTask) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}
